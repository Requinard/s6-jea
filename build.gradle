buildscript {
    ext.kotlin_version = '1.2.10'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
        classpath 'org.jboss.arquillian.gradle:arquillian-gradle-plugin:0.1'
        classpath 'com.palantir:jacoco-coverage:0.4.0'
    }
}

group 'com.terarion'
version '1.0'

apply plugin: 'java'
apply plugin: 'kotlin-platform-jvm'
apply plugin: 'kotlin-jpa'
apply plugin: "kotlin-allopen"
apply plugin: "kotlin-noarg"
apply plugin: 'war'
apply plugin: 'arquillian'

configurations {
    ktlint
}

allOpen {
    annotation("javax.ejb.Stateless")
    annotation("javax.persistence.Entity")
    annotation("javax.ejb.Singleton")
    annotation("javax.ws.rs.Path")
    annotation("utils.Open")
}

noArg {
    annotation("javax.ejb.Stateless")
    annotation("javax.persistence.Entity")
    annotation("javax.ejb.Singleton")
    annotation("javax.ws.rs.Path")
    annotation("utils.Open")
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integration/kotlin')
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        resources.srcDir file('src/integration/resources')
    }
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"

}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'com.auth0:java-jwt:3.3.0'
    compile group: 'joda-time', name: 'joda-time', version: '2.9.9'
    compile group: 'com.google.guava', name: 'guava', version: '23.5-jre'
    compile group: 'org.primefaces', name: 'primefaces', version: '6.0'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.25'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.4'
    compile group: 'com.fasterxml', name: 'jackson-module-hibernate', version: '1.9.1'
    providedCompile group: 'javax', name: 'javaee-api', version: '8.0'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-junit', version: '2.0.0.0'
    testCompile "com.nhaarman:mockito-kotlin:1.5.0"
    integrationTestCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    integrationTestCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    integrationTestCompile group: 'org.eclipse.persistence', name: 'eclipselink', version: '2.5.0'
    integrationTestCompile group: 'org.postgresql', name: 'postgresql', version: '42.2.1'
    integrationTestCompile 'io.rest-assured:rest-assured:3.0.7'
    integrationTestCompile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    ktlint "com.github.shyiko:ktlint:0.18.0"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
